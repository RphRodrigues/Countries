apply plugin: 'jacoco'

task jacocoTestReport(type: JacocoReport) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports"

    reports {
        xml.required = true
        xml.outputLocation.set(layout.buildDirectory.file("reports/jacoco/jacoco.xml"))

        html.required = true
        html.outputLocation.set(layout.buildDirectory.dir("reports/jacoco/jacocoHtml"))
    }

    classDirectories.setFrom(fileTree(
            dir: "$buildDir/tmp/kotlin-classes/debug",
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/*$ViewInjector*.*',
                       '**/BuildConfig.*',
                       '**/Manifest*.*']
    ))
    sourceDirectories.setFrom(files(["src/main/java"]))
    executionData.setFrom(files("$buildDir/jacoco/testDebugUnitTest.exec"))

    doLast {
        println "Jacoco resport has been genereted to file:/${buildDir}/reports/jacoco/jacocoHtml/index.html"
    }
}

task testDebugUnitTestCoverage() {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."

    dependsOn "testDebugUnitTest"
    finalizedBy "jacocoTestReport"
}
