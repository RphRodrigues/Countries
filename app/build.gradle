import java.nio.file.Files
import java.nio.file.Paths

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'io.gitlab.arturbosch.detekt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.appdistribution'
    id 'com.google.firebase.firebase-perf'
    id 'com.datadoghq.dd-sdk-android-gradle-plugin'
    id 'kotlin-kapt'
}

apply from: "${rootDir}/tools/secrets.gradle"
apply from: "${rootDir}/tools/detekt/detekt.gradle"
apply from: "${rootDir}/tools/jacoco/jacoco.gradle"

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()

if (!Files.exists(Paths.get(keystorePropertiesFile.getAbsolutePath()))) {
    keystorePropertiesFile.createNewFile()
}

keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 34

    defaultConfig {
        applicationId "br.com.rstudio.countries"
        minSdk 21
        targetSdkVersion 34
        versionCode 3
        versionName "1.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "int", "CACHE_IN_MB", "5"
        buildConfigField "long", "REMOTE_CONFIG_INTERVAL", "3600"
        buildConfigField "String", "BASE_URL", "\"https://restcountries.com/v3.1/\""
        buildConfigField "String", "DATADOG_CLIENT_TOKEN", "\"$DATADOG_CLIENT_TOKEN\""
        buildConfigField "String", "DATADOG_APPLICATION_ID", "\"$DATADOG_APPLICATION_ID\""

        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }

    signingConfigs {
        homolog {
            storeFile file("$rootDir/keystore")
            keyAlias keystoreProperties['firebaseAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
        }

        release {
            storeFile file("$rootDir/keystore")
            keyAlias keystoreProperties['playStoreAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            FirebasePerformance.instrumentationEnabled = false
            versionNameSuffix '-debug'
            applicationIdSuffix '.dev'

            buildConfigField "long", "REMOTE_CONFIG_INTERVAL", "60"
            firebaseAppDistribution {
                appId = "1:466387488330:android:b63f7e597527011c09e0b3"
                artifactPath = "$buildDir/outputs/apk/debug/app-debug.apk"
            }

            minifyEnabled false
            shrinkResources false
        }

        homolog {
            debuggable false
            versionNameSuffix '-homolog'
            applicationIdSuffix '.homolog'

            signingConfig signingConfigs.homolog

            firebaseAppDistribution {
                appId = "1:466387488330:android:6b197eb73af291e509e0b3"
                artifactPath = "$buildDir/outputs/apk/homolog/app-homolog.apk"
            }

            minifyEnabled true
            shrinkResources true
        }

        release {
            debuggable false

            signingConfig signingConfigs.release

            firebaseAppDistribution {
                appId = "1:466387488330:android:468c37d0760b8bb709e0b3"
                artifactPath = "$buildDir/outputs/apk/release/app-release.apk"
            }

            minifyEnabled true
            shrinkResources true
        }
    }

    firebaseAppDistribution {
        artifactType = "APK"
        serviceCredentialsFile = "$rootDir/service-account.json"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    lintOptions {
        checkReleaseBuilds false
    }

    buildFeatures {
        viewBinding true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

datadog {
    site = "US5"
}

dependencies {

    // Android
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'

    // Koin
    def koin_version = "3.4.0"
    implementation "io.insert-koin:koin-core:$koin_version"
    implementation "io.insert-koin:koin-android:$koin_version"

    // Rxjava
    implementation "io.reactivex.rxjava2:rxjava:2.2.6"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"

    // Coroutines
    def coroutines_version = "1.6.4"
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version")

    // ViewModel
    def lifecycle_version = "2.3.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // Room
    def room_version = "2.5.0"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Retrofit
    implementation "com.google.code.gson:gson:2.8.7"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.0"

    // Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Lottie
    implementation 'com.airbnb.android:lottie:6.0.0'

    // Glide
    implementation "com.github.bumptech.glide:glide:4.12.0"
    annotationProcessor "com.github.bumptech.glide:compiler:4.12.0"

    // Leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.14'

    // Shimmer
    implementation 'com.facebook.shimmer:shimmer:0.5.0'

    // Datadog
    def datadog_version = "2.18.0"
    implementation "com.datadoghq:dd-sdk-android-rum:$datadog_version"
    implementation "com.datadoghq:dd-sdk-android-logs:$datadog_version"
    implementation "com.datadoghq:dd-sdk-android-timber:$datadog_version"
    implementation "com.datadoghq:dd-sdk-android-okhttp:$datadog_version"
    implementation "com.datadoghq:dd-sdk-android-trace:$datadog_version"

    // Firebase
    implementation 'com.google.firebase:firebase-perf:20.0.6'
    implementation 'com.google.firebase:firebase-analytics-ktx:20.1.2'
    implementation 'com.google.firebase:firebase-crashlytics-ktx:18.2.9'
    implementation 'com.google.firebase:firebase-config:21.0.2'

    // Unit test
    testImplementation "junit:junit:4.12"
    testImplementation "io.insert-koin:koin-test:$koin_version"
    testImplementation "io.mockk:mockk:1.12.0"
    testImplementation "org.robolectric:robolectric:4.10.1"
    testImplementation "com.squareup.okhttp3:mockwebserver:4.9.0"
    debugImplementation "androidx.fragment:fragment-testing:1.3.6"
    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation "androidx.test.ext:junit:1.1.3"
    testImplementation "androidx.test.espresso:espresso-core:3.4.0"
    testImplementation "androidx.test.espresso:espresso-contrib:3.4.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    // Android test
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:4.9.0"
    androidTestImplementation "io.mockk:mockk-android:1.12.0"
    androidTestImplementation "io.insert-koin:koin-test:$koin_version"
}
